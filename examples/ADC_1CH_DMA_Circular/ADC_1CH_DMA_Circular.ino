/*
 * ADC_1CH_DMA_Circular
 *
 * Streams one ADC channel at 44.1 kS/s over USB serial without gaps,
 * using circular DMA with a double-buffered half/full transfer.
 *
 * Data is sent as raw 16-bit binary - use the MATLAB script in matlab/
 * to receive and plot it.
 *
 * The D86 LED blinks if the board runs into an error.
 */

#include "SensEdu.h"

/* -------------------------------------------------------------------------- */
/*                                  Settings                                  */
/* -------------------------------------------------------------------------- */

// Error Indicator LED
static const uint8_t ERROR_LED_PIN = D86;

// Half-transfer chunk size (per channel)
// (not multiples of 32/64 to flush the USB chunk)
static const uint16_t CHUNK_SIZE = 75;

// ADC Settings
static ADC_TypeDef* adc = ADC1;
static const uint16_t SAMPLING_RATE_PER_CH = 44100;

static uint8_t adc_pins[1] = {A0};

// DMA Settings
static const uint16_t DMA_BUFFER_SIZE = CHUNK_SIZE * 2;
volatile SENSEDU_DMA_BUFFER(dma_buffer, DMA_BUFFER_SIZE);

// Config Structure
SensEdu_ADC_Settings adc_settings = {
    .adc = adc,
    .pins = adc_pins,
    .pin_num = 1,

    .sr_mode = SENSEDU_ADC_SR_MODE_FIXED,
    .sampling_rate_hz = SAMPLING_RATE_PER_CH,
    
    .adc_mode = SENSEDU_ADC_MODE_DMA_CIRCULAR,
    .mem_address = (uint16_t*)dma_buffer,
    .mem_size = DMA_BUFFER_SIZE
};

/* -------------------------------------------------------------------------- */
/*                                    Setup                                   */
/* -------------------------------------------------------------------------- */

void setup() {
    Serial.begin(2000000);

    pinMode(ERROR_LED_PIN, OUTPUT);
    digitalWrite(ERROR_LED_PIN, HIGH);

    SensEdu_ADC_Init(&adc_settings);
    SensEdu_ADC_Enable(adc);
    SensEdu_ADC_Start(adc);

    check_lib_errors(ERROR_LED_PIN);
}

/* -------------------------------------------------------------------------- */
/*                                    Loop                                    */
/* -------------------------------------------------------------------------- */

void loop() {
    if (SensEdu_ADC_IsDmaHalfTransferComplete(adc)) {
        SensEdu_ADC_ClearDmaHalfTransferComplete(adc);
        if (Serial) {
            transfer_buf(&dma_buffer[0], (DMA_BUFFER_SIZE / 2));
        }
    }

    if (SensEdu_ADC_IsDmaTransferComplete(adc)) {
        SensEdu_ADC_ClearDmaTransferComplete(adc);
        if (Serial) {
            transfer_buf(&dma_buffer[DMA_BUFFER_SIZE / 2], (DMA_BUFFER_SIZE / 2));
        }
    }
}

/* -------------------------------------------------------------------------- */
/*                                  Functions                                 */
/* -------------------------------------------------------------------------- */

// Transfers selected buffer in one write
static void transfer_buf(volatile uint16_t* data, uint16_t data_length) {
    uint8_t* ptr = (uint8_t*)data;
    Serial.write(ptr, data_length * sizeof(uint16_t));
}

// Checks if the library has raised any internal errors
// Serial is busy streaming, so the error LED is used instead
static void check_lib_errors(uint8_t error_led) {
    uint32_t lib_error = SensEdu_GetError();
    while (lib_error != 0) {
        fatal_error(error_led);
    }
}

// Halts the system and blinks the error LED
static void fatal_error(uint8_t error_led) {
    digitalWrite(error_led, !digitalRead(error_led));
    delay(200);
}
